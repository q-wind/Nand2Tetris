// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=tmpX);
    Not16(in=tmpX, out=notTmpX);
    Mux16(a=tmpX, b=notTmpX, sel=nx, out=resX);
    Mux16(a=y, b=false, sel=zy, out=tmpY);
    Not16(in=tmpY, out=notTmpY);
    Mux16(a=tmpY, b=notTmpY, sel=ny, out=resY);
    
    Add16(a=resX, b=resY, out=addXY);
    And16(a=resX, b=resY, out=andXY);
    Mux16(a=andXY, b=addXY, sel=f, out=res);

    Not16(in=res, out=notRes);
    /* note that we can't use out'pin as any input, and can't use sub bus of the internal node */
    Mux16(a=res, b=notRes, sel=no, out[0..7]=lowOut, out[8..15]=highOut, out[15]=msbOut, out=out);

    Or8Way(in=lowOut, out=lowOr);
    Or8Way(in=highOut, out=highOr);
    Or(a=lowOr, b=highOr, out=or16Way);
    Mux(a=true, b=false, sel=or16Way, out=zr);
     
    Mux(a=false, b=true, sel=msbOut, out=ng);
}
